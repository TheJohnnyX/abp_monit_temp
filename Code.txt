#define A      1
#define B      2
#define C      3
#define D      4
#define E      6
#define F      7
#define G      5
#define ledon  9
#define ledt   8
#define buzz  10
#define temp  16
#define relay 14
#define swt   15
#define t2    18
#define t1    19

int unity = 0;
int ten   = 0;
int value = 0;
int tempC = 0;
int SP1   = 70;
int SP2   = 90;
int hist  = 3;

byte  input[] = {temp, swt};
byte output[] = {A, B, C, D, E, F, G, t1, t2, ledon, ledt, buzz, relay};

byte segNumAno[][8] = 
{
  {0,0,0,0,0,0,1,1}, //0
  {1,0,0,1,1,1,1,1}, //1
  {0,0,1,0,0,1,0,1}, //2
  {0,0,0,0,1,1,0,1}, //3
  {1,0,0,1,1,0,0,1}, //4
  {0,1,0,0,1,0,0,1}, //5
  {0,1,0,0,0,0,0,1}, //6
  {0,0,0,1,1,1,1,1}, //7
  {0,0,0,0,0,0,0,1}, //8 
  {0,0,0,0,1,0,0,1}, //9
};

void getTemp()
{
  value = analogRead(temp);
  
  tempC = map(value, 20, 358 , -40, 125);
  
  if(tempC >= 99)
  {
    tempC = 99;
  }
  else if(tempC <= 0)
  {
    tempC = 0;
  }
  
  ten   = tempC / 10; 
  unity = tempC % 10;
}

void setDigit(int flag)
{
  for(int i = 0; i < 8; i++)
  {
    digitalWrite(output[i],segNumAno[flag][i]);
  }
}

void multiplexing()
{
  setDigit(ten);
  digitalWrite(t1,  LOW);
  delay(1);
  digitalWrite(t1, HIGH);
    
  setDigit(unity);
  digitalWrite(t2,  LOW);
  delay(1);
  digitalWrite(t2, HIGH);
}

void powerOut()
{
  //Setpoint 1
  if(tempC >= SP1 + hist)
  {
    digitalWrite(ledt, HIGH);
  }
  if(tempC <= SP1 - hist)
  {
    digitalWrite(ledt,  LOW);
  }
  
  //Setpoint 2
  if(tempC >= SP2 + hist)
  {
    digitalWrite(relay, HIGH);
      
    tone(buzz, 690);
  }
  if(tempC <= SP2 - hist)
  {
    digitalWrite(relay,  LOW);
      
    noTone(buzz);
  }
  
  digitalWrite(ledon, HIGH);
}

void shutdown()
{
  digitalWrite(t1,   HIGH);
  digitalWrite(t2,   HIGH);
  digitalWrite(relay, LOW);
  digitalWrite(ledon, LOW);
  digitalWrite(ledt,  LOW);
  noTone(buzz);
}

void setup()
{
  for(int x = 0; x < 2; x++)
  {
    pinMode(input[x], INPUT);
  }
  for(int y = 0; y < 14; y++)
  {
    pinMode(output[y], OUTPUT);
  }
  
  digitalWrite(t1, HIGH);
  digitalWrite(t2, HIGH);
  
  //Serial.begin(9600);
} 

void loop()
{
  if(digitalRead(swt))
  {
    getTemp();
    
    multiplexing();
    
    powerOut(); 
  }
  else
  {
    shutdown();
  } 
}